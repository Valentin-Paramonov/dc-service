buildscript {
    ext {
        springBootVersion = '1.5.6.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-jooq'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    compile 'org.dizitart:nitrite:1.0.1'
    compile 'io.jsonwebtoken:jjwt:0.7.0'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.security:spring-security-test'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'net.bytebuddy:byte-buddy:1.7.1'
    testCompile 'org.objenesis:objenesis:2.6'
    testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4'
}

sourceSets {
    endpointTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/endpoint-test/groovy')
        }
    }
    integrationTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/groovy')
        }
    }
}

configurations {
    endpointTestCompile.extendsFrom testCompile
    endpointTestRuntime.extendsFrom testRuntime
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task endpointTest(type: Test) {
    testClassesDirs = sourceSets.endpointTest.output.classesDirs
    classpath = sourceSets.endpointTest.runtimeClasspath
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

build.dependsOn endpointTest
build.dependsOn integrationTest
